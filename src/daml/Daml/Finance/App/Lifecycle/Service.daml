-- Copyright (c) 2022 Digital Asset (Switzerland) GmbH and/or its affiliates. All rights reserved.
-- SPDX-License-Identifier: Apache-2.0

module Daml.Finance.App.Lifecycle.Service where

import ContingentClaims.Core.Observation (Observation(..))
import ContingentClaims.Lifecycle.Lifecycle qualified as Lifecycle
import ContingentClaims.Lifecycle.Util qualified as Util (expiry, fixings, payoffs)
import Daml.Finance.App.Interface.Base.Service qualified as Base (I, View(..))
import Daml.Finance.App.Interface.Lifecycle.Service qualified as Service (DeclareDividend(..), DeclareReplacement(..), DeclareStockSplit(..), Expiry(..), Fixings(..), GetCurrentClaims(..), HasImplementation, I, Lifecycle(..), Multipliers(..), Payoffs(..), PreviewLifecycle(..), PreviewPricing(..), SimulateLifecycle(..), Underlyings(..), View(..))
import Daml.Finance.App.Lifecycle.Pricing qualified as Pricing
import Daml.Finance.App.Structuring.Auto.Service qualified as Structuring
import Daml.Finance.Claims.Util.Lifecycle qualified as Claim (lifecycle, lifecycleClaims, timeEvent)
import Daml.Finance.Interface.Instrument.Base.Instrument qualified as Instrument (I, R)
import Daml.Finance.Interface.Instrument.Equity.Instrument qualified as Equity (I, DeclareDividend(..), DeclareReplacement(..), DeclareStockSplit(..))
import Daml.Finance.Interface.Claims.Claim qualified as Claim (getClaims, GetClaims(..))
import Daml.Finance.Interface.Lifecycle.Rule.Lifecycle qualified as Lifecycle (I, Evolve(..))
import Daml.Finance.Interface.Util.Disclosure qualified as Disclosure (I)
import Daml.Finance.Interface.Types.Common (InstrumentKey(..))

type T = Service

instance Service.HasImplementation Service

template Service
  with
    operator : Party
    provider : Party
    customer : Party
    distributionRuleCid : ContractId Lifecycle.I
    replacementRuleCid : ContractId Lifecycle.I
  where
    signatory operator, provider, customer

    key (operator, provider, customer) : (Party, Party, Party)
    maintainer key._1

    interface instance Base.I for Service where
      view = Base.View with operator; provider; customer

      terminate = pure ()

    interface instance Service.I for Service where
      view = Service.View with distributionRuleCid; replacementRuleCid

      asBase = toInterface @Base.I this

      declareDividend Service.DeclareDividend{timeObservableCid; equity; newVersion; id; description; effectiveDate; perUnitDistribution} = do
        now <- getTime
        (_, equityRef) <- fetchByKey @Instrument.R equity
        equity <- fetch equityRef.cid
        let
          disclosure = toInterface @Disclosure.I equity
          v = view equity
          dv = view disclosure
          newInstrument = InstrumentKey with depository = provider; issuer = customer; id = v.id; version = newVersion
        exerciseByKey @Structuring.Service (operator, provider, customer) Structuring.RequestAndCreateEquity with id = v.id; description = v.description; version = newVersion; validAsOf = now; observers = dv.observers
        eventCid <- exercise (coerceContractId equityRef.cid : ContractId Equity.I) Equity.DeclareDividend with id; description; effectiveDate; newInstrument; perUnitDistribution
        snd <$> exercise distributionRuleCid Lifecycle.Evolve with ruleName = "Dividend"; eventCid; timeObservableCid; observableCids = []

      declareReplacement Service.DeclareReplacement{timeObservableCid; equity; id; description; effectiveDate; perUnitReplacement} = do
        (_, equityRef) <- fetchByKey @Instrument.R equity
        eventCid <- exercise (coerceContractId equityRef.cid : ContractId Equity.I) Equity.DeclareReplacement with id; description; effectiveDate; perUnitReplacement
        snd <$> exercise replacementRuleCid Lifecycle.Evolve with ruleName = "StockSplit"; eventCid; timeObservableCid; observableCids = []

      declareStockSplit Service.DeclareStockSplit{timeObservableCid; equity; newVersion; id; description; effectiveDate; adjustmentFactor} = do
        now <- getTime
        (_, equityRef) <- fetchByKey @Instrument.R equity
        equity <- fetch equityRef.cid
        let
          disclosure = toInterface @Disclosure.I equity
          v = view equity
          dv = view disclosure
          newInstrument = InstrumentKey with depository = provider; issuer = customer; id = v.id; version = newVersion
        exerciseByKey @Structuring.Service (operator, provider, customer) Structuring.RequestAndCreateEquity with id = v.id; description = v.description; version = newVersion; validAsOf = now; observers = dv.observers
        eventCid <- exercise (coerceContractId equityRef.cid : ContractId Equity.I) Equity.DeclareStockSplit with id; description; effectiveDate; newInstrument; adjustmentFactor
        snd <$> exercise replacementRuleCid Lifecycle.Evolve with ruleName = "StockSplit"; eventCid; timeObservableCid; observableCids = []

      previewLifecycle Service.PreviewLifecycle{today; observableCids; instrumentCid} = do
        hasClaims <- fetch instrumentCid
        instrument <- fetch (coerceContractId instrumentCid : ContractId Instrument.I)
        claims <- fst <$> Claim.lifecycle customer observableCids hasClaims [Claim.timeEvent (view instrument).validAsOf]
        Claim.lifecycleClaims observableCids ((view hasClaims).acquisitionTime) claims [Claim.timeEvent today]

      simulateLifecycle Service.SimulateLifecycle{today; prices; instrumentCid} = do
        instrument <- fetch instrumentCid
        taggedClaims <- Claim.getClaims instrument $ Claim.GetClaims with actor = customer
        let
          claim = mconcat $ map (.claim) taggedClaims
          simulate price = do
            result <- Lifecycle.lifecycle (\_ t -> pure price) claim today today
            pure result.pending
        mapA simulate prices

      getCurrentClaims Service.GetCurrentClaims{instrumentCid; observableCids} = do
        hasClaims <- fetch instrumentCid
        instrument <- fetch (coerceContractId instrumentCid : ContractId Instrument.I)
        fst <$> Claim.lifecycle customer observableCids hasClaims [Claim.timeEvent (view instrument).validAsOf]

      lifecycle Service.Lifecycle{ruleName; eventCid; timeObservableCid; observableCids; lifecyclableCid} = do
        exercise lifecyclableCid Lifecycle.Evolve with ruleName; eventCid; timeObservableCid; observableCids

      expiry Service.Expiry{claims} = do
        pure $ Util.expiry claims

      underlyings Service.Underlyings{claims} = do
        pure $ snd <$> Util.payoffs claims

      payoffs Service.Payoffs{claims} = do
        pure $ Util.payoffs claims

      fixings Service.Fixings{claims} = do
        pure $ Util.fixings claims

      multipliers Service.Multipliers{claims} = do
          let getConst : (Observation Time Decimal a, b) -> [Decimal]
              getConst (Const x, _) = [x]
              getConst _            = []
          return $ concatMap getConst $ Util.payoffs claims

      previewPricing Service.PreviewPricing{ccy; claims} = do
        let formula = Pricing.valueClaim ccy claims
        pure (formula, Pricing.print formula)

template Offer
  with
    operator : Party
    provider : Party
    customer : Party
    distributionRuleCid : ContractId Lifecycle.I
    replacementRuleCid : ContractId Lifecycle.I
  where
    signatory operator, provider
    observer customer

    choice Accept : ContractId Service
      controller customer
      do
        create Service with ..

    choice Decline : ()
      controller customer
      do pure ()

    choice Withdraw : ()
      controller provider
      do pure ()

template Request
  with
    customer : Party
    provider : Party
  where
    signatory customer
    observer provider

    choice Cancel : ()
      controller customer
      do pure ()

    choice Reject : ()
      controller provider
      do pure ()

    choice Approve : ContractId Service
      with
        operator : Party
        distributionRuleCid : ContractId Lifecycle.I
        replacementRuleCid : ContractId Lifecycle.I
      controller operator, provider
      do
        create Service with ..
